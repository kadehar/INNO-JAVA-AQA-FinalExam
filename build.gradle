plugins {
    alias(libs.plugins.allure.gradle)
    alias(libs.plugins.allure.adapter)
    alias(libs.plugins.lombok)
}

allure {
    version.set("2.29.0")
    adapter {
        aspectjWeaver.set(true)
        aspectjVersion.set("1.9.23")
    }
}

allprojects {
    apply plugin: 'java-library'

    group 'github.com.kadehar'
    version '1.0'

    repositories {
        mavenCentral()
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = 'UTF-8'
    }
}

dependencies {
    // Allure
    testImplementation(libs.allure.selenide) {
        exclude group: 'com.codeborne'
    }
    testImplementation(libs.allure.junit5) {
        exclude group: "org.junit.jupiter"
    }
    // CONFIG
    testImplementation libs.owner
    // DB
    testImplementation project(":db")
    // REST
    testImplementation project(":api")
    // WEB
    testImplementation libs.selenide
    // TEST
    testImplementation libs.junit.jupiter
    testImplementation libs.assertJ
    // UTILS
    testImplementation project(":utils")
}

test {
    options {
        systemProperties(System.getProperties())
    }
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    useJUnitPlatform()
}

tasks.register("apiTest", Test) {
    options {
        systemProperties(System.getProperties())
    }
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    useJUnitPlatform {
        includeTags "api"
    }
}

tasks.register("webTest", Test) {
    options {
        systemProperties(System.getProperties())
    }
    testLogging.showStandardStreams = true
    testLogging.exceptionFormat = 'full'
    useJUnitPlatform {
        includeTags "web"
    }
}